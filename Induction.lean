-- Принцип индукции для индуктивных типов

open Bool
open Nat

-- Каждому определённому командой inductive типу соответствует принцип индукции.
-- Он гласит: если известно действие некоторой функции на каждый конструктор типа,
-- то известно действие этой функции на этот тип вообще.

-- Например, принцип индукции для Bool:
-- (здесь строка с дефисами это комментарий, она написана для красоты и не
-- является частью программы)
def BoolRec : (C : Bool → Sort u)
              → C false
              → C true
              --------------------
              → (t : Bool) → C t
  | C, x, y, false => x
  | C, x, y, true  => y

#print Bool.rec


-- Принцип индукции для Nat.
-- Это "принцип математической индукции".
def NatInd : (C : Nat → Sort u)
           → C zero
           → (∀ {n}, C n → C (succ n))
           ---------------------------
           → (∀ t, C t)
  | _, z, _, zero   => z
  | C, z, f, succ n => f (NatInd C z f n)

#print Nat.rec


-- Принцип индукции для равенства.

def EqInduction : {C : {x y : α} → x = y → Sort u}
                    → ((x : α) → C (Eq.refl x))
                    -------------------------------
                    → ∀ (x y : α) (t : x = y), C t
  | C, f, x, _, rfl => f x

#print Eq.rec
